Attribute VB_Name = "HighLevel"
Option Explicit

Dim oTrack As OwnshipLeg
Dim tTrack As OwnshipLeg
Dim bearingData As Leg
Dim oLegs As Collection


Const startDate = #1/1/1900 10:00:00 PM#

Sub LoadData()
    Dim aSheet As Worksheet
    Set aSheet = ActiveWorkbook.Worksheets("Analysis")
    
    ' clear the output cells
    aSheet.Range("ownship_data").Clear
    aSheet.Range("target_data").Clear
    aSheet.Range("sensor_data").Clear
    aSheet.Range("ownship_legs").Clear
    aSheet.Range("bearing_legs").Clear
    aSheet.Range("target_legs").Clear

    ' retrieve the sample rate
    Dim sampleRate As Double
    sampleRate = aSheet.Range("sample_rate").value

    ' get the ownship data
    Set oTrack = Collate.getTrackFromRange("OwnshipTrack", "Ownship", 6, sampleRate)
    
    ' and the target track
    Set tTrack = Collate.getTrackFromRange("TargetTrack", "Target", 14, sampleRate)
        
    ' now get the sensor data
    Set bearingData = Collate.getSensorDataFromRange("A3:K50000", aSheet.Range("SensorName").value, sampleRate)

    ' share the results
    aSheet.Range("ownship_data").value = oTrack.times.Count
    aSheet.Range("sensor_data").value = bearingData.times.Count
    aSheet.Range("target_data").value = tTrack.times.Count
    
    ' output the ownship course data
    Collate.outputTrack oTrack, "F11"
    
    ' output the target course data
    Collate.outputTrack tTrack, "H11"
    
    ' output the bearings
    Collate.outputBearings bearingData, "J11"
    
End Sub

Sub CalcOwnshipLegs()
    
    ' clear the output
    Dim aSheet As Worksheet
    Set aSheet = ActiveWorkbook.Worksheets("Analysis")
    aSheet.Range("ownship_legs").Clear
    
    ' we don't actually rely on the ownship data for the legs,
    ' we're reading in hard-coded values.
    ' But, let's pretend we are using the data
    If oTrack Is Nothing Then
        MsgBox "Ownship data must be loaded"
        Exit Sub
    End If
    
    Dim oSheet As Worksheet
    Set oSheet = ActiveWorkbook.Worksheets("Ownship Legs")
    
    ' get the ownship data
    Dim oTrackIn As Range
    Set oTrackIn = oSheet.Range("ownshipLegRef")
    
    Set oLegs = New Collection
    
    Dim thisR As Range
    For Each thisR In oTrackIn.Rows
        Dim tName As String
        Dim tStart As Double
        Dim tEnd As Double
    
        tName = thisR.Cells(1, 1)
        tStart = thisR.Cells(1, 2)
        tEnd = thisR.Cells(1, 3)
    
        Dim thisT As TimePeriod
        Set thisT = Factory.createTimePeriod(tName, tStart, tEnd)
        
        oLegs.add thisT
        
    Next thisR
    
    ' output the results
    aSheet.Range("ownship_legs").value = oLegs.Count

End Sub

Sub CalcTargetLegs()
    
    Dim aSheet As Worksheet
    Set aSheet = ActiveWorkbook.Worksheets("Analysis")

    ' clear the output
    aSheet.Range("bearing_legs").Clear
    aSheet.Range("target_legs").Clear
    
    ' check data
    If oLegs Is Nothing Then
        MsgBox "Need to calculate ownship legs"
        Exit Sub
    End If

    If bearingData Is Nothing Then
        MsgBox "Need to load bearing data"
        Exit Sub
    End If

    ' ok, slice the bearing data according to ownship legs
    Dim bLegs As Legs
    Set bLegs = bearingData.sliceWith(oLegs)
    
    ' output the results
    aSheet.Range("bearing_legs").value = bLegs.Items.Count
    
    ' now work through the bearing legs
    Dim targetLegs As Legs
    Set targetLegs = bLegs.determineTargetLegs
    
    ' output the results
    aSheet.Range("target_legs").value = targetLegs.Items.Count
End Sub

'Sub DoCalc_Click()
'    ' populate list of sensor data
'    Dim sensorLegs As Legs
'    sensorLegs = Collate.getSensorLegs()
'
'    ' ok, now slice them
 '   Dim targetLegs As Legs
 ''   Set targetLegs = sensorLegs.determineTargetLegs
 '
 '   ' also output the component calculations
 '   Collate.showCalculations targetLegs
 '
 '   ' ok, now share the good news
 '   Collate.showTargetLegs targetLegs
'End Sub

Public Function Date2Long(dtmDate As Date) As Long
    
    Date2Long = (dtmDate - #1/1/1900 10:00:00 PM#) * 86400
End Function
